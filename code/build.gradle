plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'signing'
    id 'org.graalvm.buildtools.native' version '0.11.3'
    id 'idea'
    id 'net.researchgate.release' version '3.1.0'
}

group = 'io.github.crramirez'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

idea {
    module {
        downloadSources = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // JLine terminal for platform-agnostic raw mode support
    implementation "org.jline:jline-terminal:${jlineVersion}"
    implementation "org.jline:jline-terminal-jni:${jlineVersion}"

    testImplementation platform('org.junit:junit-bom:5.10.1')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.8.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.8.0'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = "full"
        showStandardStreams = false
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-Xlint', '-Xdiags:verbose', '-Xlint:-requires-automatic']
}

// Main library JAR: exclude demo/** content (matches compiled paths like demo/**)
tasks.jar {
    exclude('demo/**')
    manifest {
        attributes(
                'Implementation-Version': project.version
        )
    }
}

// Demo fat JAR: includes EVERYTHING (library + demo + dependencies), not published
tasks.register('jarDemo', Jar) {
    group = 'build'
    description = 'Assembles a fat JAR containing casciian/**, demo/**, and all runtime dependencies.'

    from(sourceSets.main.output)

    // Include all runtime dependencies (including jline) for a fat JAR
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    // Handle duplicate files from dependencies
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveBaseName.set('casciian-demo')
    manifest {
        attributes(
                'Main-Class': 'demo.Demo1',
                'Implementation-Version': project.version
        )
    }
}

// Ensure assemble also builds the demo jar
tasks.assemble {
    dependsOn tasks.named('jarDemo')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'casciian'
                description = 'Casciian - Java Text User Interface library'
                url = 'https://github.com/crramirez/casciian'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'crramirez'
                        name = 'crramirez'
                        url = 'https://github.com/crramirez'
                    }
                }
                scm {
                    url = 'https://github.com/crramirez/casciian'
                    connection = 'scm:git:https://github.com/crramirez/casciian.git'
                    developerConnection = 'scm:git:ssh://git@github.com/crramirez/casciian.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = "localStaging"
            url = layout.buildDirectory.dir("staging-repo")
        }
    }
}

def signingKeyId = (findProperty("signingKeyId") ?: System.getenv("SIGNING_KEY_ID"))?.toString()
def signingKeyB64 = (findProperty("signingKeyB64") ?: System.getenv("SIGNING_KEY_B64"))?.toString()
def signingPassword = (findProperty("signingPassword") ?: System.getenv("SIGNING_PASSWORD"))?.toString()

def signingKey = null
if (signingKeyB64) {
    signingKey = new String(Base64.decoder.decode(signingKeyB64), "UTF-8")
}

signing {
    if (signingKeyId && signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign publishing.publications
    }
}

tasks.register('publishToLocalStaging') {
    group = 'publishing'
    description = 'Publishes signed artifacts to build/staging-repo (for Central Portal bundling).'
    dependsOn(
            'publishMavenJavaPublicationToLocalStagingRepository'
    )
}

tasks.register('centralBundleZip', Zip) {
    group = 'publishing'
    description = 'Creates a Maven-layout ZIP bundle for Sonatype Central Portal upload.'
    dependsOn('publishToLocalStaging')

    from(layout.buildDirectory.dir("staging-repo"))
    archiveBaseName.set("casciian-central")
    archiveVersion.set(project.version.toString())
    destinationDirectory.set(layout.buildDirectory.dir("central-bundle"))

    // Result: build/central-bundle/casciian-central-<version>.zip
}

tasks.javadoc {
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.author = true
    options.version = true
    options.links('https://docs.oracle.com/en/java/javase/21/docs/api/')
}

// -----------------------------------------------------------------------------
// Use jarDemo on the native-image classpath (Native Build Tools 0.10.x)
// -----------------------------------------------------------------------------
def jarDemoTaskProvider = tasks.named('jarDemo', Jar)

// -----------------------------------------------------------------------------
// GraalVM native-image via official plugin
// - `nativeCompile` / `nativeBuild` create a native executable under build/native
// -----------------------------------------------------------------------------
graalvmNative {
    toolchainDetection = true
    binaries {
        main {

            javaLauncher = javaToolchains.launcherFor {
                languageVersion = JavaLanguageVersion.of(25)
            }

            imageName = 'casciian'
            mainClass = 'demo.Demo1'
            buildArgs.add('-march=compatibility')
            buildArgs.add('-Os')
            buildArgs.add('--future-defaults=all')
            buildArgs.add('--enable-native-access=ALL-UNNAMED')
            buildArgs.addAll(providers.provider {
                [
                    '-cp',
                    jarDemoTaskProvider.get().archiveFile.get().asFile.absolutePath
                ]
            })
        }
    }
}

tasks.named('nativeCompile') {
    dependsOn(jarDemoTaskProvider)
}

// -----------------------------------------------------------------------------
// DEB and RPM packaging using fpm
// -----------------------------------------------------------------------------
def nativeBinaryPath = layout.buildDirectory.file('native/nativeCompile/casciian').get().asFile
def packagingDir = layout.buildDirectory.dir('packaging').get().asFile
def debOutputDir = layout.buildDirectory.dir('distributions/deb').get().asFile
def rpmOutputDir = layout.buildDirectory.dir('distributions/rpm').get().asFile
def packageVersion = project.version.toString().replace('-SNAPSHOT', '')

tasks.register('preparePackaging') {
    group = 'distribution'
    description = 'Prepares directory structure for packaging (requires native binary)'
    dependsOn tasks.named('nativeCompile')

    doFirst {
        if (!nativeBinaryPath.exists()) {
            throw new GradleException("Native binary not found at ${nativeBinaryPath}. Run './gradlew nativeCompile' first to build the native executable.")
        }
    }

    doLast {
        delete packagingDir
        def binDir = new File(packagingDir, 'usr/bin')
        binDir.mkdirs()

        copy {
            from nativeBinaryPath
            into binDir
            rename { 'casciian-demo' }
        }

        // Make the binary executable
        new File(binDir, 'casciian-demo').setExecutable(true, false)
    }
}

tasks.register('buildDeb', Exec) {
    group = 'distribution'
    description = 'Builds DEB package using fpm'
    dependsOn preparePackaging

    doFirst {
        debOutputDir.mkdirs()
    }

    commandLine 'fpm',
            '-s', 'dir',
            '-t', 'deb',
            '-n', 'casciian-demo',
            '-v', packageVersion,
            '--iteration', '1',
            '-a', 'amd64',
            '--description', 'Casciian Demo - Java Text User Interface demo application',
            '--url', 'https://github.com/crramirez/casciian',
            '--maintainer', 'crramirez',
            '--license', 'Apache-2.0',
            '-C', packagingDir.absolutePath,
            '-p', "${debOutputDir.absolutePath}/casciian-demo_${packageVersion}-1_amd64.deb",
            '.'
}

tasks.register('buildRpm', Exec) {
    group = 'distribution'
    description = 'Builds RPM package using fpm'
    dependsOn preparePackaging

    doFirst {
        rpmOutputDir.mkdirs()
    }

    commandLine 'fpm',
            '-s', 'dir',
            '-t', 'rpm',
            '-n', 'casciian-demo',
            '-v', packageVersion,
            '--iteration', '1',
            '-a', 'x86_64',
            '--description', 'Casciian Demo - Java Text User Interface demo application',
            '--url', 'https://github.com/crramirez/casciian',
            '--maintainer', 'crramirez',
            '--license', 'Apache-2.0',
            '-C', packagingDir.absolutePath,
            '-p', "${rpmOutputDir.absolutePath}/casciian-demo-${packageVersion}-1.x86_64.rpm",
            '.'
}

tasks.register('buildPackages') {
    group = 'distribution'
    description = 'Builds both DEB and RPM packages (requires native binary)'
    dependsOn buildDeb, buildRpm
}

release {
    preTagCommitMessage = '[release] prepare release '
    tagCommitMessage = '[release] '
    newVersionCommitMessage = '[release] prepare next '
    tagTemplate = 'v${version}'
}

tasks.named('afterReleaseBuild') {
    dependsOn 'jarDemo', 'publishToLocalStaging', 'centralBundleZip', 'nativeCompile', 'buildPackages'
}